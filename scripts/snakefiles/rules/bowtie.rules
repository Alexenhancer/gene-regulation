# Set parameters
if not "qsub" in config.keys():
    sys.exit("The parameter qsub must be defined in the config file")

if not "version" in config["genome"].keys():
    sys.exit("Genome version must be defined in the config file")

if not "fasta_file" in config["genome"].keys():
    sys.exit("Fasta_file must be defined in the config file")

if not "genome" in config["dir"].keys():
    sys.exit("Genome dir must be defined in the dir section of the config file")

if not "seq_type" in config["metadata"].keys():
    sys.exit("Sequencing type (single or paired ends) must be defined in the config file")

if config["metadata"]["seq_type"] == "pe" and not "strands" in config["metadata"].keys():
    sys.exit("The param metadata/strands must be defined when analysing paired end data")

if not "bowtie" in config.keys():
    config["bowtie"] = {}

if not "threads" in config["bowtie"].keys():
    config["bowtie"]["threads"] = "1"

if not "max_mismatches" in config["bowtie"].keys():
    config["bowtie"]["max_mismatches"] = "0"

# Define paths
if not "fastq" in config["dir"].keys():
    sys.exit("You must define a location for the fastq file.")

genome_version = config["genome"]["version"]
genome_dir = config["dir"]["genome"] + "/"
genome_file = genome_dir + config["genome"]["fasta_file"]
bowtie_dir = genome_dir + "bowtie/"
bowtie_index = bowtie_dir + genome_version + ".fa"


# Define input files
def bowtie_inputs(wildcards):
    seq_type = config["metadata"]["seq_type"]
    if (seq_type == "pe"):
        reads = expand("{fastq_dir}/{samples}/{prefix}_{strand}.fastq", strand=config["metadata"]["strands"].split(), samples=wildcards.samples, fastq_dir=config["dir"]["fastq"], prefix=wildcards.prefix)
    elif (seq_type == "se"):
        reads = expand("{fastq_dir}/{samples}/{prefix}.fastq", samples=wildcards.samples, fastq_dir=config["dir"]["fastq"], prefix=wildcards.prefix)
    else:
        sys.exit("Error: invalid sequencing type parameter. Must be 'se' or 'pe'")
    return reads

# Rule
rule bowtie:
    """Read mapping for single end data using bowtie. 
    Requires the indexing to have previously been done (using the
    rule bowtie_index).

    Required parameters:
        config["genome"]["version"]
        config["genome"]["fasta_file"]
        config["qsub"]
        config["bowtie"]["max_mismatches"]

    Optional parameters:
        config["bowtie"]["dir"]
        config["bowtie"]["threads"]

    Contributors: 
        Jacques van Helden, Lucie Khamvongsa, Claire Rioualen
    """
    input:
        reads = bowtie_inputs, 
        index = bowtie_index
    output:
        bam = config["dir"]["samples"] + "/{samples}/{prefix}_bowtie.bam"
    params:
        sam = config["dir"]["samples"] + "/{samples}/{prefix}_bowtie.sam", \
        max_mismatches = config["bowtie"]["max_mismatches"],
        threads = config["bowtie"]["threads"],
        qsub = config["qsub"] \
          + " -e " + config["dir"]["samples"] + "/{samples}/{prefix}_bowtie_qsub.err" \
          + " -o " + config["dir"]["samples"] + "/{samples}/{prefix}_bowtie_qsub.out"
    log: config["dir"]["samples"] + "/{samples}/{prefix}_bowtie.log"
    benchmark: config["dir"]["samples"] + "/{samples}/{prefix}_bowtie_benchmark.json"
    run:
        if (seq_type == "pe"):
            shell("bowtie -t -q -S -n {params.max_mismatches} --threads {params.threads} --phred33-quals {input.index} -1 " + input.reads[0] + " -2 " + input.reads[1] + " {params.sam} 2> {log}")
        elif (seq_type == "se"):
            shell("bowtie -t -q -S -n {params.max_mismatches} --threads {params.threads} --phred33-quals {input.index} {input.reads} {params.sam} 2> {log}")
        shell("samtools view -b -S {params.sam} > {output.bam} 2> {log}")
        shell("rm {params.sam}")

