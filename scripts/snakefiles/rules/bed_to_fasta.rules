

rule bed_to_fasta:
	"""Fetch sequences from UCSC to obtain a fasta file from a set of
	genomic coordinates described in a bed file.

	Example: 
	mkdir -p test/fetch_seq; cd test/fetch_seq; 
	wget http://pedagogix-tagc.univ-mrs.fr/rsat/demo_files/fetch-sequences_Schmidt_2011_mm9_CEBPA_SWEMBL_R0.12_702peaks.bed; 
	cd -
	snakemake --snakefile ${RSAT}/snakemake_files/chip-seq_motifs.py test/fetch_seq/fetch-sequences_Schmidt_2011_mm9_CEBPA_SWEMBL_R0.12_702peaks.fasta

        Parameters:

        config["qsub"] : qsub options

        config["rsat"]["verbosity"] verbosity level used for the RSAT program fetch-sequences

        config["genome"]["ucsc_id"] identifier of the genome of interest on UCSC genome browser

	"""
	input: "{regions}.bed"
	output: "{regions}.fasta"
	log: "{regions}_fetchseq.log"  ## Log content depends on fetch-sequences verbosity option
	benchmark: "{regions}_fetchseq_benchmark.json"
	params: verbosity = config["rsat"]["verbosity"], \
                genome = config["genome"]["ucsc_id"], \
                qsub = config["qsub"] + " -q short -e {regions}_fetchseq_qsub.err -o {regions}_fetchseq_qsub.out"
	shell:"fetch-sequences -v {params.verbosity} -i {input} -header_format galaxy " \
            + " -genome {params.genome} -o {output} 2> {log}"
