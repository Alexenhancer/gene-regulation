"""Trim one a paired end raw reads file with sickle.

Required parameters:
    config["qsub"]
Optional parameters:
    config["sickle"]["threshold"]
    config["sickle"]["qual"]

Usage:


Authors:
    Claire Rioualen, Lucie Khamvongsa, Jacques van Helden
"""

# Set defaults params in case they're not defined in config file
if not "qsub" in config.keys():
    sys.exit("The parameter qsub must be defined in the config file")

if not "sickle" in config.keys():
    config["sickle"] = {}

if not "threshold" in config["sickle"].keys():
    config["sickle"]["threshold"] = "20"

if not "qual" in config["sickle"].keys():
    config["sickle"]["qual"] = "sanger"

# Define paths
if not "samples" in config["dir"].keys():
    config["dir"]["samples"] = config["dir"]["results"]

suffix = "sickle-pe-q" + config["sickle"]["threshold"]                          # check consistency with the workflow file


rule sickle_pe:
    input:
        forward = "{reads}_R1.fastq", \
        reverse = "{reads}_R2.fastq"
    output:
        forward = "{reads}_" + suffix + "_R1.fastq", \
        reverse = "{reads}_" + suffix + "_R2.fastq", \
        single = "{reads}_" + suffix + ".fastq"
    params:
        threshold = config["sickle"]["threshold"], \
        qual_type = config["sickle"]["qual"], \
        qsub = config['qsub'] \
            + " -e "+ "{reads}_" + suffix + "_qsub.err" \
            + " -o "+ "{reads}_" + suffix + "_qsub.out"
    log: "{reads}_" + suffix + ".log"
    benchmark: "{reads}_" + suffix + "_benchmark.json"
    shell: "sickle pe --pe-file1 {input.forward} --pe-file2 {input.reverse} --qual-type {params.qual_type} --qual-threshold {params.threshold} --output-pe1 {output.forward} --output-pe2 {output.reverse} --output-single {output.single} 2> {log}"
