"""Trim reads required before alignment using the sickle software.

"""

rule sickle_paired_ends:
    """Trim paired-end reads (fastq.gz format) using sickle.

    This script requires to specify the following parameters in the
    snakemake config file.

      config["sickle"]["qual_type"]
      config["sickle"]["threshold"]
      config["sickle"]["other_options"]
      config["suffix"]["reads_fwd"]
      config["suffix"]["reads_rev"]
      config["qsub"]


    The quality threshold (threshold) and the quality type (qual_type)
    parameters have to be determined in the config file (json format)
    linked in the snakefile (workflow). Any other parameters in the
    other_options will be passed to sickle.

    The rule produces three output read files in fastq format:
    
    forward reads
    reverse reads
    single reads: unmatched between the forward and reverse strands.

    The sickle summary is stored in a separate file.

    """
    
    input: forward="{reads}" + config["suffix"]["reads_fwd"] + ".fastq.gz", reverse="{reads}" + config["suffix"]["reads_rev"] + ".fastq.gz"
    output: forward = "{reads}" + config["suffix"]["reads_fwd"] + "_sickle_pe_q" + config["sickle"]["threshold"] + ".fastq.gz", \
            reverse = "{reads}" + config["suffix"]["reads_rev"] + "_sickle_pe_q" + config["sickle"]["threshold"] + ".fastq.gz", \
            single = "{reads}_single_sickle_pe_q" + config["sickle"]["threshold"] + ".fastq.gz", \
            summary = "{reads}_sickle_pe_q" + config["sickle"]["threshold"] + "_summary.txt"
    params: threshold = config["sickle"]["threshold"], \
            qual_type=config["sickle"]["qual_type"], \
            options = config["sickle"]["other_options"], \
            qsub = config["qsub"] \
            + " -e {reads}_sickle_pe_q" + config["sickle"]["threshold"] + "_qsub.err" \
            + " -o {reads}_sickle_pe_q" + config["sickle"]["threshold"] + "_qsub.out"
    log: "{reads}_sickle_pe_q" + config["sickle"]["threshold"] + ".log"
    benchmark: "{reads}_sickle_pe_q" + config["sickle"]["threshold"] + "_benchmark.json"
    message: "Trimming paired-ends raw reads with sickle\n\tInput:\t{input}\n\tOutput:\t{output}\n\tLog:\t{log}"
    shell: "sickle pe --gzip-output --pe-file1 {input.forward} --pe-file2 {input.reverse} --qual-type {params.qual_type} --qual-threshold {params.threshold} --output-pe1 {output.forward} --output-pe2 {output.reverse} --output-single {output.single} {params.options} > {output.summary} 2> {log}"

"""
    Trim reads required before alignement 

"""

